import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id 'net.researchgate.release' version '2.1.0'
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	
	repositories {
		mavenCentral()
	}
}

def scriptsDir = "${buildDir}/scripts"
def appendixDir = "${buildDir}/appendix"

task cleanTestOutput(type: Delete) {
	delete "${project.rootDir}/ics"
}

clean.dependsOn 'cleanTestOutput'
clean.dependsOn 'cleanCopyTestFiles'


task copyTestFiles (dependsOn: 'processTestResources', type: Copy) {
	from "${sourceSets.test.output.resourcesDir}/vcs"
	into "${project.rootDir}/vcs"
}

test.dependsOn 'copyTestFiles'

task fatJar (type: Jar) {
	outputs.upToDateWhen { return false }
	baseName = shortName
	manifest {
		attributes(	'Implementation-Title': project.name,
					'Implementation-Version': version,
					'Main-Class': 'net.sourceforge.users.dragomerlin.vcs2icsCalendarConverter.Main')
	}
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task processScripts (dependsOn: 'fatJar', type: Copy) {
	outputs.upToDateWhen { return false }
	
	from ("${project.rootDir}/src/main/bat") {
		filter(ReplaceTokens, tokens: [archiveName: fatJar.archiveName])
	}
	into scriptsDir
}

task processAppendix (dependsOn: 'processTestResources', type: Copy) {

	into appendixDir 
	from ('licence.md')
	from ('authors.md')
	from ('changelog.md')
	from ('readme.md')
	doLast {
		into ('vcs') {
			from "${sourceSets.test.output.resourcesDir}/vcs"
		}
	}
}

task buildReleaseZip(dependsOn: ['processScripts', 'processAppendix'], type: Zip) {
	outputs.upToDateWhen { return false }
	baseName = shortName
	
	def files = files(
		fatJar.archivePath, 
		scriptsDir,
		appendixDir)
	from files
}

afterReleaseBuild.dependsOn 'buildReleaseZip'


release {
	tagTemplate = 'v$version'
}

dependencies {
	compile 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
	compile 'commons-codec:commons-codec:1.7'
	compile 'commons-io:commons-io:2.4'
}
		
eclipse.classpath {
	file {
		beforeMerged { classpath -> 
			def jreEntries = classpath.entries.findAll { entry ->
				entry.kind.equals('con') &&
				entry.path.contains('org.eclipse.jdt.launching.JRE_CONTAINER')
			}
			classpath.entries.removeAll(jreEntries)
		}
	}
	if (JavaVersion.current() > targetCompatibility) {
		containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		containers "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-$sourceCompatibility"
	}
}


eclipseJdt {
	enabled = false
}